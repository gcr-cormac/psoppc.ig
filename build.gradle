plugins {
    id 'application'
	id 'java'
	id 'java-library'
	id 'eclipse'
    id 'maven-publish'
}

application {
    applicationDefaultJvmArgs = ['-Dgreeting.language=en']
    mainClass = 'org.psoppc.fhir.AHRQProfiler'
}

group = 'org.psoppc.fhir'
version = '0.0.1' // Set project-level property
def applicationName = 'psoppc.ig' // Local variable for the artifact name


java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenLocal()
    mavenCentral()
    flatDir {
	    dirs 'libs'
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/main/resources'
           }
    }
}

configurations.all {
    resolutionStrategy {
        force 'org.slf4j:slf4j-api:2.0.0'
    }
}

dependencies {
    implementation 'org.hl7.fhir.emf:fhir.serdeser:0.0.1'
	implementation 'org.hl7:fhir.emf:4.3.0'
	implementation 'org.eclipse.emf:org.eclipse.emf.common:2.40.0'
	implementation 'org.eclipse.emf:org.eclipse.emf.ecore:2.38.0'
	implementation 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.38.0'

   	implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.6'
   	
   	// Logging
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
	configurations.all {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }
    
    testImplementation 'org.everit.json:org.everit.json.schema:1.5.0'
    testImplementation 'com.networknt:json-schema-validator:1.0.42'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

jar {
    manifest {
        attributes 'Main-Class': provider { application.mainClass.get() }
    }
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE  // Ignore duplicate entries

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude("META-INF/*.RSA", "META-INF/*.DSA", "META-INF/*.SF", "META-INF/MANIFEST.MF")
}

processResources {
  from ('src/main/java') {
     include '**/*.ecore'
  }
}
 
test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

task checkSignedJars {
    doLast {
        println "Scanning for signed JARs..."
        configurations.runtimeClasspath.files.each { jarFile ->
            if (jarFile.name.endsWith(".jar")) {
                println "Checking: ${jarFile.name}"
                def hasSignature = false
                zipTree(jarFile).visit { file ->
                    if (file.name.matches("META-INF/.*\\.(SF|RSA|DSA|EC)")) {
                        println "Found signed JAR: ${jarFile.name} (Signature File: ${file.name})"
                        hasSignature = true
                    }
                }
                if (!hasSignature) {
                    println "No signature found in: ${jarFile.name}"
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId applicationName
            version project.version
            from(components.java)
        }
	}
}
